plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    all {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}

bootJar {
    destinationDirectory = file(layout.buildDirectory)
}

dependencies {
    implementation project(':common')

    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    compileOnly "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "com.querydsl:querydsl-jpa:${rootProject.ext.queryDslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${rootProject.ext.queryDslVersion}:jakarta"
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
    testImplementation 'com.h2database:h2'

    implementation 'commons-codec:commons-codec'

    implementation "io.jsonwebtoken:jjwt-api:${rootProject.ext.jjwtApiVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${rootProject.ext.jjwtApiVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${rootProject.ext.jjwtApiVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    implementation 'org.springframework.boot:spring-boot-starter-activemq'

    implementation 'org.apache.tika:tika-core:3.2.0'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    implementation 'org.springframework.boot:spring-boot-starter-mail'
}

def generated = layout.buildDirectory.dir("generated/querydsl").get().asFile

sourceSets {
    main {
        java {
            srcDirs += generated
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

tasks.named('test') {
    useJUnitPlatform()
}
